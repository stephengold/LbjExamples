// Gradle script to build the LbjExamples/apps sub-project

// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application' // to build a Java application
}

ext {
    // which BTF (buildType + flavor) of the native physics library to use:
    //btf = 'DebugSp'
    btf = 'ReleaseSp'

    fs = System.getProperty('file.separator')
    downloadsDir = System.getProperty('user.home') + fs + 'Downloads' + fs

    // URL from which native physics libraries should be copied:
    libbulletjmeUrl = "https://github.com/stephengold/Libbulletjme/releases/download/$lbjVersion/"
    //libbulletjmeUrl = "file:///home/sgold/NetBeansProjects/Libbulletjme/dist/"
}

// tasks to run specific apps:

tasks.register('AppChooser', JavaExec) {
    description 'Runs the AppChooser app.'
    mainClass = 'com.github.stephengold.lbjexamples.AppChooser'
}

// physics demos
tasks.register('ConveyorDemo', JavaExec) {
    description 'Runs the ConveyorDemo app.'
    mainClass = 'com.github.stephengold.lbjexamples.ConveyorDemo'
}
tasks.register('NewtonsCradle', JavaExec) {
    description 'Runs the NewtonsCradle demo app.'
    mainClass = 'com.github.stephengold.lbjexamples.NewtonsCradle'
}
tasks.register('Pachinko', JavaExec) {
    description 'Runs the Pachinko demo app.'
    mainClass = 'com.github.stephengold.lbjexamples.Pachinko'
}
tasks.register('SplitDemo', JavaExec) {
    description 'Runs the SplitDemo app.'
    mainClass = 'com.github.stephengold.lbjexamples.SplitDemo'
}
tasks.register('TestGearJoint', JavaExec) {
    description 'Runs the TestGearJoint app.'
    mainClass = 'com.github.stephengold.lbjexamples.TestGearJoint'
}
tasks.register('ThousandCubes', JavaExec) {
    description 'Runs the ThousandCubes demo app.'
    mainClass = 'com.github.stephengold.lbjexamples.ThousandCubes'
}
tasks.register('Windlass', JavaExec) {
    description 'Runs the Windlass demo app.'
    mainClass = 'com.github.stephengold.lbjexamples.Windlass'
}

// physics console apps (no graphics)
tasks.register('HelloLibbulletjme', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.console.HelloLibbulletjme'
}
tasks.register('HelloVehicle0', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.console.HelloVehicle0'
}
tasks.register('SpeedTest0', JavaExec) {
    args 0
    mainClass = 'com.github.stephengold.lbjexamples.apps.console.SpeedTest'
}
tasks.register('SpeedTest1', JavaExec) {
    args 1
    mainClass = 'com.github.stephengold.lbjexamples.apps.console.SpeedTest'
}
tasks.register('SpeedTest2', JavaExec) {
    args 2
    mainClass = 'com.github.stephengold.lbjexamples.apps.console.SpeedTest'
}

// physics tutorial apps (very simple)
tasks.register('HelloCcd', JavaExec) {
    description 'Runs the HelloCcd tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloCcd'
}
tasks.register('HelloCharacter', JavaExec) {
    description 'Runs the HelloCharacter tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloCharacter'
}
tasks.register('HelloCloth', JavaExec) {
    description 'Runs the HelloCloth tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloCloth'
}
tasks.register('HelloContactResponse', JavaExec) {
    description 'Runs the HelloContactResponse tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloContactResponse'
}
tasks.register('HelloDamping', JavaExec) {
    description 'Runs the HelloDamping tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloDamping'
}
tasks.register('HelloDeactivation', JavaExec) {
    description 'Runs the HelloDeactivation tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloDeactivation'
}
tasks.register('HelloDoubleEnded', JavaExec) {
    description 'Runs the HelloDoubleEnded tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloDoubleEnded'
}
tasks.register('HelloGhost', JavaExec) {
    description 'Runs the HelloGhost tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloGhost'
}
tasks.register('HelloJoint', JavaExec) {
    description 'Runs the HelloJoint tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloJoint'
}
tasks.register('HelloKinematics', JavaExec) {
    description 'Runs the HelloKinematics tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloKinematics'
}
tasks.register('HelloLimit', JavaExec) {
    description 'Runs the HelloLimit tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloLimit'
}
tasks.register('HelloMadMallet', JavaExec) {
    description 'Runs the HelloMadMallet tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloMadMallet'
}
tasks.register('HelloMassDistribution', JavaExec) {
    description 'Runs the HelloMassDistribution tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloMassDistribution'
}
tasks.register('HelloMinkowski', JavaExec) {
    description 'Runs the HelloMinkowski tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloMinkowski'
}
tasks.register('HelloMotor', JavaExec) {
    description 'Runs the HelloMotor tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloMotor'
}
tasks.register('HelloNonUniformGravity', JavaExec) {
    description 'Runs the HelloNonUniformGravity tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloNonUniformGravity'
}
tasks.register('HelloPin', JavaExec) {
    description 'Runs the HelloPin tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloPin'
}
tasks.register('HelloRigidBody', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloRigidBody'
}
tasks.register('HelloServo', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloServo'
}
tasks.register('HelloSoftBody', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloSoftBody'
}
tasks.register('HelloSoftRope', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloSoftRope'
}
tasks.register('HelloSoftSoft', JavaExec) {
    description 'Runs the HelloSoftSoft tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloSoftSoft'
}
tasks.register('HelloSport', JavaExec) {
    description 'Runs the HelloSport tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloSport'
}
tasks.register('HelloSpring', JavaExec) {
    description 'Runs the HelloSpring tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloSpring'
}
tasks.register('HelloStaticBody', JavaExec) {
    description 'Runs the HelloStaticBody tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloStaticBody'
}
tasks.register('HelloVehicle', JavaExec) {
    description 'Runs the HelloVehicle tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloVehicle'
}
tasks.register('HelloWalk', JavaExec) {
    description 'Runs the HelloWalk tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloWalk'
}
tasks.register('HelloWind', JavaExec) {
    description 'Runs the HelloWind tutorial app.'
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloWind'
}

// graphics-only tests (no physics)
tasks.register('AssimpTest', JavaExec) {
    description 'Runs the Assimp test app.'
    mainClass = 'com.github.stephengold.sport.test.AssimpTest'
}
tasks.register('CheckerboardTest', JavaExec) {
    description 'Runs the checkerboard test app.'
    mainClass = 'com.github.stephengold.sport.test.CheckerboardTest'
}
tasks.register('ClipspaceTest', JavaExec) {
    description 'Runs the clipspace test app.'
    mainClass = 'com.github.stephengold.sport.test.ClipspaceTest'
}
tasks.register('DynamicMeshTest', JavaExec) {
    description 'Runs the dynamic-mesh test app.'
    mainClass = 'com.github.stephengold.sport.test.DynamicMeshTest'
}
tasks.register('IcosphereTest', JavaExec) {
    description 'Runs the icosphere test app.'
    mainClass = 'com.github.stephengold.sport.test.IcosphereTest'
}
tasks.register('MouseTest', JavaExec) {
    description 'Runs the first mouse test app.'
    mainClass = 'com.github.stephengold.sport.test.MouseTest'
}
tasks.register('MouseTest2', JavaExec) {
    description 'Runs the 2nd mouse test app.'
    mainClass = 'com.github.stephengold.sport.test.MouseTest2'
}
tasks.register('OctasphereTest', JavaExec) {
    description 'Runs the octasphere test app.'
    mainClass = 'com.github.stephengold.sport.test.OctasphereTest'
}
tasks.register('RainbowTest', JavaExec) {
    description 'Runs the rainbow test app.'
    mainClass = 'com.github.stephengold.sport.test.RainbowTest'
}
tasks.register('SpriteTest', JavaExec) {
    description 'Runs the sprite test app.'
    mainClass = 'com.github.stephengold.sport.test.SpriteTest'
}
tasks.register('TextureTest', JavaExec) {
    description 'Runs the texture test app.'
    mainClass = 'com.github.stephengold.sport.test.TextureTest'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem
Boolean includeLinux = os.isLinux()
Boolean includeMacOsX = os.isMacOsX()
Boolean includeWindows = os.isWindows()

tasks.withType(JavaExec) { // Java runtime options:
    if (os.isMacOsX()) {
        jvmArgs '-XstartOnFirstThread' // required for GLFW on macOS
    }
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'

    if (includeLinux) {
        dependsOn 'downloadLinux64'
        dependsOn 'downloadLinux_ARM32'
        dependsOn 'downloadLinux_ARM64'
    }
    if (includeMacOsX) {
        dependsOn 'downloadMacOSX64'
        dependsOn 'downloadMacOSX_ARM64'
    }
    if (includeWindows) {
        dependsOn 'downloadWindows32'
        dependsOn 'downloadWindows64'
    }
}

application {
    mainClass = 'com.github.stephengold.lbjexamples.AppChooser'
}
if (!hasProperty('mainClass')) {
    ext.mainClass = application.mainClass
}
jar.manifest.attributes('Main-Class': application.mainClass)

dependencies {
    if (includeLinux) {
        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-linux"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-linux"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux-arm32"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-linux-arm32"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux-arm32"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-linux-arm32"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux-arm64"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-linux-arm64"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux-arm64"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-linux-arm64"
    }

    if (includeMacOsX) {
        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-macos"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-macos"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-macos"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-macos"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-macos-arm64"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-macos-arm64"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-macos-arm64"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-macos-arm64"
    }

    if (includeWindows) {
        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-windows"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-windows"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows-x86"
        runtimeOnly "org.lwjgl:lwjgl-assimp:${lwjglVersion}:natives-windows-x86"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows-x86"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-windows-x86"
    }

    implementation project(':common')
}

// tasks to download/clean the native physics library for each platform:

registerPlatformTasks('Linux64',     '_libbulletjme.so')
registerPlatformTasks('Linux_ARM32', '_libbulletjme.so')
registerPlatformTasks('Linux_ARM64', '_libbulletjme.so')

registerPlatformTasks('MacOSX64',     '_libbulletjme.dylib')
registerPlatformTasks('MacOSX_ARM64', '_libbulletjme.dylib')

registerPlatformTasks('Windows32', '_bulletjme.dll')
registerPlatformTasks('Windows64', '_bulletjme.dll')

// helper method to register 'download' and 'clean' tasks:

void registerPlatformTasks(String platform, String suffix) {
    String filename = platform + btf + suffix

    String cleanTaskName = 'clean' + platform
    clean.dependsOn(cleanTaskName)
    tasks.register(cleanTaskName, Delete) {
        delete downloadsDir + filename
    }

    tasks.register('download' + platform, MyDownload) {
        sourceUrl = libbulletjmeUrl + filename
        target = file(downloadsDir + filename)
    }
}

// helper class to wrap Ant download task:

class MyDownload extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
        println sourceUrl
    }
}
