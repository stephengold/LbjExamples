// Gradle script to build the LbjExamples/apps sub-project

plugins {
    id 'application'
}

ext {
    // which BTF (buildType + flavor) of the native physics library to use:
    btf = 'ReleaseSp'

    fs = System.getProperty('file.separator')
    downloadsDir = System.getProperty('user.home') + fs + 'Downloads' + fs

    // URL from which native physics libraries should be copied:
    libbulletjmeUrl = "https://github.com/stephengold/Libbulletjme/releases/download/$lbjVersion/"
}

// tasks to run individual apps:

tasks.register('AppChooser', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.AppChooser'
}

tasks.register('HelloCcd', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloCcd'
}
tasks.register('HelloCharacter', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloCharacter'
}
tasks.register('HelloContactResponse', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloContactResponse'
}
tasks.register('HelloDamping', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloDamping'
}
tasks.register('HelloDeactivation', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloDeactivation'
}
tasks.register('HelloKinematics', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloKinematics'
}
tasks.register('HelloLibbulletjme', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloLibbulletjme'
}
tasks.register('HelloMadMallet', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloMadMallet'
}
tasks.register('HelloMassDistribution', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloMassDistribution'
}
tasks.register('HelloNonUniformGravity', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloNonUniformGravity'
}
tasks.register('HelloRigidBody', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloRigidBody'
}
tasks.register('HelloStaticBody', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloStaticBody'
}
tasks.register('HelloVehicle', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloVehicle'
}
tasks.register('HelloVehicle0', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloVehicle0'
}
tasks.register('HelloWalk', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.HelloWalk'
}
tasks.register('NewtonsCradle', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.NewtonsCradle'
}
tasks.register('ScreenSpaceTest', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.ScreenSpaceTest'
}
tasks.register('ThousandCubes', JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.apps.ThousandCubes'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem
Boolean includeLinux = os.isLinux()
Boolean includeMacOsX = os.isMacOsX()
Boolean includeWindows = os.isWindows()

tasks.withType(JavaExec) { // Java runtime options:
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'

    if (includeLinux) {
        dependsOn 'downloadLinux64'
        dependsOn 'downloadLinux_ARM32'
        dependsOn 'downloadLinux_ARM64'
    }
    if (includeMacOsX) {
        dependsOn 'downloadMacOSX64'
        dependsOn 'downloadMacOSX_ARM64'
    }
    if (includeWindows) {
        dependsOn 'downloadWindows32'
        dependsOn 'downloadWindows64'
    }
}

mainClassName = 'com.github.stephengold.lbjexamples.apps.AppChooser'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    implementation project(':common')

    if (includeLinux) {
        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-linux"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux-arm32"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux-arm32"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-linux-arm32"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-linux-arm64"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-linux-arm64"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-linux-arm64"
    }

    if (includeMacOsX) {
        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-macos"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-macos"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-macos"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-macos-arm64"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-macos-arm64"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-macos-arm64"
    }

    if (includeWindows) {
        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-windows"

        runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows-x86"
        runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows-x86"
        runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-windows-x86"
    }
}

// tasks to download/clean the native physics library for each platform:

registerPlatformTasks('Linux64',     '_libbulletjme.so')
registerPlatformTasks('Linux_ARM32', '_libbulletjme.so')
registerPlatformTasks('Linux_ARM64', '_libbulletjme.so')

registerPlatformTasks('MacOSX64',     '_libbulletjme.dylib')
registerPlatformTasks('MacOSX_ARM64', '_libbulletjme.dylib')

registerPlatformTasks('Windows32', '_bulletjme.dll')
registerPlatformTasks('Windows64', '_bulletjme.dll')

// helper method to register 'download' and 'clean' tasks:

void registerPlatformTasks(String platform, String suffix) {
    String filename = platform + btf + suffix

    String cleanTaskName = 'clean' + platform
    clean.dependsOn(cleanTaskName)
    tasks.register(cleanTaskName, Delete) {
        delete downloadsDir + filename
    }

    tasks.register('download' + platform, MyDownload) {
        sourceUrl = libbulletjmeUrl + filename
        target = file(downloadsDir + filename)
    }
}

// helper class to wrap Ant download task:

class MyDownload extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
        println sourceUrl
    }
}
