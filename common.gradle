// Gradle settings and tasks common to all LbjExamples subprojects

apply plugin: 'java'

project.ext.lwjglVersion = '3.3.1'

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform
OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;
if(os.isWindows())
    project.ext.lwjglNatives = 'natives-windows'
else if(os.isMacOsX())
    project.ext.lwjglNatives = 'natives-macos'
else if(os.isLinux())
    project.ext.lwjglNatives = 'natives-linux'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
}

dependencies {
    implementation 'com.github.stephengold:Libbulletjme:14.3.0'
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation 'org.joml:joml:1.10.4'
    implementation 'org.lwjgl:lwjgl'
    implementation 'org.lwjgl:lwjgl-glfw'
    implementation 'org.lwjgl:lwjgl-opengl'

    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
}
