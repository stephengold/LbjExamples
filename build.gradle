// Gradle script to build the LbjExamples project

plugins {
    id 'application'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_14)) {
        // Suppress warnings that source value 7 is obsolete.
        options.compilerArgs << '-Xlint:-options'
    }
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
    options.encoding = 'UTF-8'
}

// run the apps

task HelloLibbulletjme(type: JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.HelloLibbulletjme'
}

task HelloVehicle(type: JavaExec) {
    mainClass = 'com.github.stephengold.lbjexamples.HelloVehicle'
}

tasks.withType(JavaExec) { // Java runtime options:
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

mainClassName = 'com.github.stephengold.lbjexamples.HelloLibbulletjme'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    //mavenLocal()
    mavenCentral()
}

dependencies {
    // from mavenCentral (or mavenLocal) repositories:
    implementation 'com.github.stephengold:Libbulletjme:13.0.0'
}
